
//РЕДАКТОР MONACO

Процедура ПослеЗаписиФайлаМакета(ДопПараметры) Экспорт
	Попытка
		Файл = Новый ЧтениеZipФайла(ДопПараметры.КаталогИсходников + "monaco.zip");
		Файл.ИзвлечьВсе(ДопПараметры.КаталогИсходников);
		
		ВыполнитьОбработкуОповещения(Новый ОписаниеОповещения("ОбработкаИнициализации", ДопПараметры.Форма, ДопПараметры), Истина);
	Исключение
		ВывестиОшибку("Не удалось извлечь исходники" + Символы.ПС + ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры

Процедура ПослеПроверкиСуществованияКаталога(Существует, ДопПараметры) Экспорт
	Если Существует Тогда
		ИмяФайла = ДопПараметры.КаталогИсходников + "monaco.zip";
		ДанныеМакета = ПолучитьИзВременногоХранилища(ДопПараметры.АдресМакета);
		ДанныеМакета.НачатьЗапись(Новый ОписаниеОповещения("ПослеЗаписиФайлаМакета", Web1C_РедакторКлиент, ДопПараметры), ИмяФайла);
	Иначе
		ВывестиОшибку("Не удалось создать каталог для исходников");
	КонецЕсли;
КонецПроцедуры

Процедура ПослеСозданияКаталога(ИмяКаталога, ДопПараметры) Экспорт
	ФайлНаДиске = Новый Файл(ДопПараметры.КаталогИсходников);
	ФайлНаДиске.НачатьПроверкуСуществования(Новый ОписаниеОповещения("ПослеПроверкиСуществованияКаталога", Web1C_РедакторКлиент, ДопПараметры));
КонецПроцедуры

Процедура ПослеПроверкиСуществованияФайла(Существует, ДопПараметры) Экспорт
	Если Не Существует Тогда
		НачатьСозданиеКаталога(Новый ОписаниеОповещения("ПослеСозданияКаталога", Web1C_РедакторКлиент, ДопПараметры), ДопПараметры.КаталогИсходников);
	Иначе
		ВыполнитьОбработкуОповещения(Новый ОписаниеОповещения("ОбработкаИнициализации", ДопПараметры.Форма, ДопПараметры), Истина);
	КонецЕсли;
КонецПроцедуры

Процедура ПриПолученииКаталогаВременныхФайлов(ИмяКаталога, ДопПараметры) Экспорт
	ДопПараметры.КаталогИсходников = ИмяКаталога + "monaco\";
	
	Файл = Новый Файл(ДопПараметры.КаталогИсходников + "index.html");
	Файл.НачатьПроверкуСуществования(Новый ОписаниеОповещения("ПослеПроверкиСуществованияФайла", Web1C_РедакторКлиент, ДопПараметры));
КонецПроцедуры

Процедура ИзвлечьИсходники(АдресМакета, Форма) Экспорт
	ДопПараметры = Новый Структура("АдресМакета, КаталогИсходников, Форма", АдресМакета, "", Форма);
	
	НачатьПолучениеКаталогаВременныхФайлов(Новый ОписаниеОповещения("ПриПолученииКаталогаВременныхФайлов", Web1C_РедакторКлиент, ДопПараметры));
КонецПроцедуры

Процедура ВывестиОшибку(Текст)
	
	ПоказатьПредупреждение(, Текст);
	
КонецПроцедуры

Процедура УстановитьТекстМонако(Монако, Текст) Экспорт 
	Монако.setText(Текст, Неопределено, false);
КонецПроцедуры

Функция ОчиститьТекстМонако(Монако) Экспорт 
	Возврат Монако.eraseText();
КонецФункции

Функция ПолучитьВыделенныйТекстМонако(Монако) Экспорт
	Возврат Монако.getSelectedText();
КонецФункции

Функция ПолучитьТекстМонако(Монако) Экспорт
	Возврат Монако.getText();
КонецФункции

Процедура ПереключитьТему(Editor, Тема) Экспорт 
	Editor.setTheme(Тема);
КонецПроцедуры

Процедура ЗагрузитьТекстВРедактор(Монако, Шаблон) Экспорт 
	УстановитьТекстМонако(Монако, Шаблон);
КонецПроцедуры

Процедура ИнициализироватьРедакторMonaco(Монако, Режим) Экспорт 
	Монако.setOption("autoResizeEditorLayout", Истина);
	Монако.setOption("renderQueryDelimiters", Истина);
	Монако.setOption("generateModificationEvent", Истина);
	Монако.setLanguage(Режим);
КонецПроцедуры

//РЕДАКТОР MONACO
